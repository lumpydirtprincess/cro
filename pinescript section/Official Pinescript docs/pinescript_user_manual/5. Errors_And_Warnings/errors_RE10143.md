![](../5. Errors_And_Warnings/errors_re10143.md)

## [The requested historical offset (X) is beyond the historical buffer’s limit (Y)](../5. Errors_And_Warnings/errors_re10143.md#the-requested-historical-offset-x-is-beyond-the-historical-buffers-limit-y)

In Pine Script®, a single script executes from start to end on each bar of the chart. After each execution on a confirmed bar, Pine’s runtime system _commits (saves)_ data for a script’s variables and expressions on that bar to _fixed-sized_ [historical buffers](../3. Language/language_execution-model.md#historical-buffers). The script can retrieve past bar values from these buffers by using the [`[]` history-referencing operator](../3. Language/language_operators.md#-history-referencing-operator) or the functions that reference history internally. For example, the expression `myVar[500]` retrieves the last saved value of the `myVar` variable as of 500 bars back.

By default, the runtime system automatically sizes a script’s buffers to store only what the script requires for the historical references that it performs across historical bars:

- The system first checks the historical references that the script executes on the first 244 bars of the dataset, then sets an initial buffer size based on those references. If the script uses constant historical offsets, or if the historical offsets used on subsequent bars do not exceed the buffer sizes, the script’s historical references work without issues.
- If the script requests data from beyond an initial buffer’s limit while executing across other historical bars, the system _increases_ that buffer’s size and _restarts_ the script. This process repeats as necessary until the system reaches the restart limit or the script loads successfully without exceeding a buffer’s boundaries.

This runtime error can occur in some rare cases if the system fails to determine the appropriate size of a buffer on historical bars after restarting a script several times. However, the error more commonly occurs when a script executes out-of-bounds historical references on [realtime bars](../3. Language/language_execution-model.md#realtime-bars), because the system _does not_ continue to resize historical buffers after the script loads.

For example, the following script plots the value of [close](../../reference manual/variables/close.md) variable from 500 bars back on historical bars, then attempts to plot the value from _1000_ bars back on realtime bars. The script initially loads successfully across the chart’s history. However, the script halts and raises the runtime error once a _new realtime tick_ becomes available. The error is also reproducible by enabling [Bar Replay](https://www.tradingview.com/support/solutions/43000712747-bar-replay-how-and-why-to-test-a-strategy-in-the-past/) and pressing “Forward” once. The automatic historical buffer for the [close](../../reference manual/variables/close.md) series in this example contains only **500** past values. It does _not_ contain 1000, because the script does not reference values from 1000 bars back on any _historical_ bars, and the system does not resize the buffer on realtime bars. Consequently, the historical offset of 1000 on the first realtime bar is _beyond_ the historical buffer’s limit:

```pine
//@version=6
indicator("Historical buffer error on realtime bars demo")

// This code works as expected on historical bars, but it fails with an error on the first *realtime tick*.
// On historical bars, the maximum historical offset that the script requires is 500 bars back, so the system sets the
// buffer size for the `close` series to include only 500 past values. As such, an offset of 1000 is *out of bounds*.
float myVar = close[barstate.ishistory ? 500 : 1000]
plot(myVar)
```

To resolve such errors, programmers must ensure that their scripts’ historical buffers have a sufficient size to accommodate historical references on all bars. See below to learn more.

### [Potential fixes](../5. Errors_And_Warnings/errors_re10143.md#potential-fixes)

The following sections explain the different ways to explicitly define appropriate buffer sizes to accommodate historical references. Using these techniques is necessary only if a script’s historical references cause this runtime error. In most other cases, we typically recommend relying on automatic buffer sizing.

#### [Use the ​`max_bars_back()`​ function](../5. Errors_And_Warnings/errors_re10143.md#use-the-max_bars_back-function)

The built-in [max\_bars\_back()](../../reference manual/functions/max_bars_back.md) function explicitly sets the initial size of the historical buffer for a specified variable. If automatic buffer sizing fails, or if a script references deeper history on only realtime bars, programmers can use this function to specify appropriate buffers for specific series to accommodate historical references for up to 5000 bars back.

For example, to resolve the error in our initial script above, we can add the call `max_bars_back(close, 1000)` to specify that the buffer for the [close](../../reference manual/variables/close.md) series stores at least the latest 1000 past values. With the appropriate buffer size explicitly defined, the script performs its historical references successfully on realtime bars without raising an error:

```pine
//@version=6
indicator("Use `max_bars_back()` demo")

//@variable The `close` value from 500 bars back on historical bars, and 1000 bars back on realtime bars.
float myVar = close[barstate.ishistory ? 500 : 1000]
plot(myVar)

// This call prevents the runtime error. It specifies that the buffer for the `close` series stores data for
// 1000 previous bars, which is the *minimum* historical depth that the script requires on realtime bars.
max_bars_back(close, 1000)
```

Note that:

- Scripts can include a [max\_bars\_back()](../../reference manual/functions/max_bars_back.md) function call at any location in the script. In our example above, the call occurs _after_ the historical reference on the [close](../../reference manual/variables/close.md) variable.

#### [Use the ​`max_bars_back`​ parameter](../5. Errors_And_Warnings/errors_re10143.md#use-the-max_bars_back-parameter)

The `max_bars_back` parameter of the [indicator()](../../reference manual/functions/indicator.md) and [strategy()](../../reference manual/functions/strategy.md) functions is an alternative to [max\_bars\_back()](../../reference manual/functions/max_bars_back.md) that defines the initial size of _all_ series in a script if it has a specified argument. This parameter can offer convenience in cases where multiple series require manually defined buffers. However, it’s crucial to understand that increasing the size of every historical buffer in a script can _negatively_ impact its runtime performance and memory consumption. Therefore, for resource efficiency, we typically recommend using the [max\_bars\_back()](../../reference manual/functions/max_bars_back.md) function to size only specific buffers instead of sizing every buffer with this parameter. The only exception is if _all_ or _most_ of the series in the script actually require historical buffers with a specific size and the runtime system fails to determine that size automatically.

#### [Use the larger historical reference on early bars](../5. Errors_And_Warnings/errors_re10143.md#use-the-larger-historical-reference-on-early-bars)

An alternative way to increase the size of a specific historical buffer is to use the _largest_ required offset in historical references on _early bars_, regardless of whether the script requires that offset on those bars. The runtime system analyzes that historical offset while the script loads and sets the buffer’s size accordingly.

For example, the script version below includes the offset of 1000 in the history-referencing operation on the _first bar_, where the [barstate.isfirst](../../reference manual/variables/barstate.isfirst.md) value is `true`. This change makes no practical difference in the historical output, because the operation returns [na](../../reference manual/variables/na.md) for any nonzero offset on the first bar. However, it resolves the error because it causes the script to request data for 1000 bars back on a _historical bar_, forcing the system to create a buffer that maintains the latest 1000 past [close](../../reference manual/variables/close.md) values:

```pine
//@version=6
indicator("Larger historical reference on early bars demo")

// This code works as expected on historical and realtime bars.
// Setting the offset to 1000 on the first bar forces the system to create a 1000-bar buffer for the `close` series.
float myVar = close[barstate.isfirst ? 1000 : barstate.ishistory ? 500 : 1000]
plot(myVar)
```

### [Historical buffer errors in realtime drawings](../5. Errors_And_Warnings/errors_re10143.md#historical-buffer-errors-in-realtime-drawings)

A common cause of the historical offset error is the creation of [drawing objects](../3. Language/language_type-system.md#drawing-types) that anchor to the _past_ while the script executes on _realtime bars_. All Pine drawings that rely on chart coordinates convert their x-coordinates into _timestamps_ by referencing the [time](../../reference manual/variables/time.md) series internally, even if the programmer defines those coordinates using [bar\_index](../../reference manual/variables/bar_index.md) values.

Therefore, if a script draws into the past on realtime bars and raises this error, the typical solution is to set an explicit buffer size for the [time](../../reference manual/variables/time.md) variable.

For example, the script below draws a horizontal [line](../2. Visuals/visuals_lines-and-boxes.md#lines) from the bar at `bar_index - 500` to the current bar while executing on realtime bars only. The script loads successfully across historical bars, but it quickly halts and raises the historical offset error on the first _realtime tick_. Although the script does not explicitly use the [\[\]](../../reference manual/operators/[].md) operator, the line drawing itself references the [time](../../reference manual/variables/time.md) series internally to convert its `x1` and `x2` coordinates. Converting the `x1` coordinate to a timestamp requires access to the [time](../../reference manual/variables/time.md) value from _500 bars back_. However, the default buffer for that series stores fewer than 500 past values, because the script does not request the value from 500 bars back while it _loads_ across the historical dataset:

```pine
//@version=6
indicator("Realtime buffer error from a drawing demo")

if barstate.isrealtime
    // This call causes an error on the first realtime tick.
    // Pine drawings internally convert index-based x-coordinates into *timestamps* by referencing values
    // from the `time` series. The coordinate `bar_index - 500` converts to `time[500]` internally, but the default
    // buffer in this example stores data for *fewer* than 500 past `time` values.
    line.new(x1 = bar_index - 500, y1 = close, x2 = bar_index, y2 = close)
```

To resolve the error, programmers must use any of the techniques described in the [Potential fixes](../5. Errors_And_Warnings/errors_re10143.md#potential-fixes) section to explicitly set the size of the historical buffer for the [time](../../reference manual/variables/time.md) series. The simplest solution is to use the [max\_bars\_back()](../../reference manual/functions/max_bars_back.md) function to set the appropriate buffer size.

In the script version below, we added the call `max_bars_back(time, 500)` to specify that the buffer for the [time](../../reference manual/variables/time.md) series stores values for the latest 500 past bars. This change prevents the script from requesting a value that is outside the buffer’s boundaries when converting the `bar_index - 500` coordinate into a timestamp:

```pine
//@version=6
indicator("Preventing time-related buffer errors in realtime drawings demo")

if barstate.isrealtime
    // Draws a horizontal line from the bar 500 bars back to the current bar.
    line.new(x1 = bar_index - 500, y1 = close, x2 = bar_index, y2 = close)

// This call ensures that the buffer for the `time` series contains a sufficient history for the line drawing to convert
// its `x1` coordinate into a timestamp. With an appropriate buffer size defined, no error occurs.
max_bars_back(time, 500)
```

[Previous 
**RE10139**](../5. Errors_And_Warnings/errors_re10139.md)